buildscript {
   repositories {
      mavenCentral()
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-release" }
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-vendor" }
      maven { url "https://plugins.gradle.org/m2/" }
      //mavenLocal()
   }
   dependencies {
      classpath "gradle.plugin.us.ihmc:ihmc-build:0.9.12"
      classpath "us.ihmc:rosidl-typesupport-ihmcpubsub-java:0.2"
      classpath "us.ihmc:ihmc-pub-sub-generator:0.3.7"
   }
}

apply plugin: "us.ihmc.ihmc-build"

ihmc {
   group = "us.ihmc"
   version = "0.3"
   vcsUrl = "https://github.com/ihmcrobotics/ihmc-interfaces"
   openSource = true

   configureDependencyResolution()
   resourceDirectory("main", "idl")
   resourceDirectory("main", "rosidl")
   javaDirectory("main", "generated-java")
   configurePublications()
}

mainDependencies {
   compile group: 'us.ihmc', name: 'euclid-core', version: '0.4.6'
   compile group: 'us.ihmc', name: 'euclid', version: '0.6.0'
   compile group: 'us.ihmc', name: 'IHMCPubSub', version: '0.3.4.1'
}

task show << {
   buildscript.configurations.classpath.each { println it }
}

task generateROSTypeSupport(type: us.ihmc.ros2.rosidl.RosInterfaceGeneratorTask) {
   rosPackages = files("src/main/rosidl/common_interfaces", "src/main/rosidl/ihmc_interfaces")
   outputDirectory = file("src/main/generated-java")
   customIDL = files("src/main/idl")
}

compileJava.dependsOn generateROSTypeSupport

sourceSets.main.java.srcDirs += ['src/main/java-generated']