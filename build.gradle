buildscript {
   repositories {
      mavenCentral()
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-release" }
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-vendor" }
      maven { url "https://plugins.gradle.org/m2/" }
      mavenLocal()
      jcenter()
   }
   dependencies {
      classpath "us.ihmc:ihmc-build:0.12.8"
      classpath "us.ihmc:rosidl-typesupport-ihmcpubsub-java:0.2"
      classpath "us.ihmc:ihmc-pub-sub-generator:0.3.7"
      classpath "org.ajoberstar:grgit:2.1.0"
   }
}

apply plugin: "us.ihmc.ihmc-build"
apply plugin: "org.ajoberstar.grgit"

ihmc {
   group = "us.ihmc"
   version = "0.3"
   vcsUrl = "https://github.com/ihmcrobotics/ihmc-interfaces"
   openSource = true

   configureDependencyResolution()
   resourceDirectory("main", "idl")
   resourceDirectory("main", "rosidl")
   javaDirectory("main", "generated-java")
   configurePublications()
}

mainDependencies {
   compile group: 'us.ihmc', name: 'euclid-core', version: '0.4.13'
   compile group: 'us.ihmc', name: 'euclid', version: '0.7.5'
   compile group: 'us.ihmc', name: 'ihmc-pub-sub', version: '0.4.0'
   compile group: 'us.ihmc', name: 'ros2-common-interfaces', version: '0.4.0'
}

task show {
   doLast {
      buildscript.configurations.classpath.each { println it }
   }
}

task generateMessages(type: us.ihmc.ros2.rosidl.RosInterfaceGeneratorTask) {
   doFirst {
      delete("build/tmp/generateMessages")

      setupVendoredRepo("build/tmp/generateMessages/vendor/rcl_interfaces", "https://github.com/ros2/rcl_interfaces.git", "0.4.0")
      setupVendoredRepo("build/tmp/generateMessages/vendor/common_interfaces", "https://github.com/ros2/common_interfaces.git", "0.4.0")
   }

   rosPackages = files("build/tmp/generateMessages/vendor/rcl_interfaces",
                       "build/tmp/generateMessages/vendor/common_interfaces",
                       "src/main/rosidl/ihmc_interfaces")
   outputDirectory = file("build/tmp/generateMessages/java")
   customIDL = files("src/main/idl")

   doLast {
      copy {
         from "build/tmp/generateMessages/java/controller_msgs"
         into "src/main/generated-java/controller_msgs"
      }
   }
}

private void setupVendoredRepo(String clonePath, String vcsUrl, String tagName)
{
   delete(clonePath)

   if (!file(clonePath).exists())
   {
      def grgit = org.ajoberstar.grgit.Grgit.clone {
         dir = file(clonePath)
         uri = vcsUrl
         refToCheckout = tagName
      }
   }
}
